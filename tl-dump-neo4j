#!/usr/bin/env perl
# Copyright 2018 Norbert Preining
# This file is licensed under the GNU General Public License version 2
# or any later version.
# 
# Outputs files that can be imported into a Neo4j database using neo4j-import
#
# TODO
# for now we ignore .win32 packages


my $Master;

BEGIN {
  $^W = 1;
  unshift (@INC, "./tlpkg");
}

use strict;
use TeXLive::TLPOBJ;
use TeXLive::TLPDB;
use TeXLive::TLUtils;
use Getopt::Long;
use File::Path qw/make_path/;

my $opt_initial = 0;
my $opt_output  = "./out";
my $opt_date = "";

GetOptions(
  "initial!" => \$opt_initial,
  "output=s" => \$opt_output,
  "date=s"   => \$opt_date,
) or die "Cannot parse command line: $!";


exit (&main());


sub main
{
  my %pkgs;
  my %fls;

  my $idstr = ($opt_initial ? ":ID" : "");
  my $intstr = ($opt_initial ? ":int" : "");
  my $boolstr = ($opt_initial ? ":boolean" : "");
  my $datestr = ($opt_initial ? ":datetime" : "");

  my $tlpdb = TeXLive::TLPDB->new();
  $tlpdb->from_file($ARGV[0], verify => 0, media => "local_uncompressed");
  die "Cannot init tlpdb from $ARGV[0] ..." unless defined($tlpdb);

  # try to get basic information of tlpdb
  my $tlpdbrevision = $tlpdb->config_revision;
  if (! $tlpdbrevision) {
    print STDERR "Cannot find revision of TLPDB, bailing out!\n";
    exit 1;
  }

  foreach my $pkg ($tlpdb->list_packages) {
    next if ($pkg =~ m/^00texlive/);  # ignore internal pkgs.
    # ignore *all* arch dep pacakges (also tlpsv etc.)
    next if ($pkg ne "texlive.infra" && $pkg =~ m/\./);

    my $tlp = $tlpdb->get_package($pkg);
    if (!defined($tlp)) {
      warn "strange, $pkg not found but listed, continuing anyway";
      next;
    }
   
    $pkgs{$pkg}{'category'} = $tlp->category;
    $pkgs{$pkg}{'revision'} = $tlp->revision;
    $pkgs{$pkg}{'depends'} = {};
    my @deps = $tlp->depends;
    for my $d (@deps) {
      $pkgs{$pkg}{'depends'}{$d} = 1;
    }

    # go over files
    for my $f ($tlp->docfiles) {
      $fls{$f}{'contained'}{$pkg} = 'doc';
    }
    for my $f ($tlp->runfiles) {
      $fls{$f}{'contained'}{$pkg} = 'run';
    }
    for my $f ($tlp->srcfiles) {
      $fls{$f}{'contained'}{$pkg} = 'src';
    }
    my %foo = %{$tlp->binfiles};
    for my $a (keys %foo) {
      for my $f (@{$foo{$a}}) {
        $fls{$f}{'contained'}{$pkg} = 'bin';
      }
    }
    $pkgs{$pkg}{'uuid'} = $pkg . ":" . $pkgs{$pkg}{'revision'};
  }

  if (! -d $opt_output) {
    make_path($opt_output) || die("Cannot create output directory $opt_output: $?");
  }
  my %fd;
  open($fd{'TLPDB'}, ">:encoding(UTF-8)", "$opt_output/node-TLPDB.csv") || die("Cannot open $opt_output/node-TLPDB.csv: $!");
  print { $fd{'TLPDB'} } "uuid$idstr,revision$intstr,date$datestr,current$boolstr\n";
  print { $fd{'TLPDB'} } "tlpdb:$tlpdbrevision,$tlpdbrevision,$opt_date,true\n";

  if ($opt_initial) {
    open($fd{'TLPOBJ'}, ">:encoding(UTF-8)", "$opt_output/node-TLPOBJ.csv") || die("Cannot open $opt_output/node-TLPOBJ.csv: $!");
    print { $fd{'TLPOBJ'} } "uuid$idstr,name,revision$intstr,:LABEL\n";
    open($fd{'TLPSRC'}, ">:encoding(UTF-8)", "$opt_output/node-TLPSRC.csv") || die("Cannot open $opt_output/node-TLPSRC.csv: $!");
    print { $fd{'TLPSRC'} } "name$idstr,:LABEL\n";
  } else {
    for my $f (qw/Package Collection Scheme ConTeXt TLCore/) {
      open($fd{$f}, ">:encoding(UTF-8)", "$opt_output/node-$f.csv") || die("Cannot open $opt_output/node-$f.csv: $!");
      print { $fd{$f} } "name,revision$intstr\n";
    }
  }

  open($fd{'File'}, ">:encoding(UTF-8)", "$opt_output/node-File.csv") || die("Cannot open $opt_output/node-File.csv: $!");
  print { $fd{'File'} } "name$idstr\n";
  open($fd{'depends'}, ">:encoding(UTF-8)", "$opt_output/edge-depends.csv") || die("Cannot open $opt_output/edge-depends.csv: $!");
  open($fd{'contains'}, ">:encoding(UTF-8)", "$opt_output/edge-contains.csv") || die("Cannot open $opt_output/edge-contains.csv: $!");
  open($fd{'includes'}, ">:encoding(UTF-8)", "$opt_output/edge-includes.csv") || die("Cannot open $opt_output/edge-includes.csv: $!");

  if ($opt_initial) {
    print { $fd{'depends'} } ":START_ID,:END_ID\n";
    print { $fd{'contains'} } ":START_ID,:END_ID\n";
    print { $fd{'includes'} } ":START_ID,type,:END_ID\n";
  } else {
    print { $fd{'depends'} } "apkg,aver,bpkg,bver\n";
    print { $fd{'contains'} } "tlpdbver,pkg,ver\n";
    print { $fd{'includes'} } "pkg,ver,type,file\n";
  }

  if ($opt_initial) {
    open($fd{'instanceOf'}, ">:encoding(UTF-8)", "$opt_output/edge-instanceOf.csv") || die("Cannot open $opt_output/edge-instanceOf.csv: $!");
    print { $fd{'instanceOf'} } ":START_ID,:END_ID\n";
  }

  for my $pkg (keys %pkgs) {
    my $cat = $pkgs{$pkg}{'category'};
    my $uid = $pkgs{$pkg}{'uuid'};
    my $ver = $pkgs{$pkg}{'revision'};
    if ($opt_initial) {
      print { $fd{'TLPOBJ'} } "$uid,$pkg,$ver,$cat\n";
      print { $fd{'TLPSRC'} } "$pkg,$cat\n";
      print { $fd{'contains'} } "tlpdb:$tlpdbrevision,$uid\n";
      print { $fd{'instanceOf'} } "$uid,$pkg\n";
    } else {
      print { $fd{$cat}        } "$pkg,$ver\n";
      print { $fd{'contains'} } "$tlpdbrevision,$pkg,$ver\n";
    }
    for my $dep (keys %{$pkgs{$pkg}{'depends'}}) {
      next if ($dep =~ m/\.ARCH$/);
      next if ($dep =~ m/\.win32$/);
      if ($opt_initial) {
        my $depuuid = $pkgs{$dep}{'uuid'};
        if ($depuuid) {
          print { $fd{'depends'} } "$uid,$depuuid\n";
        } else {
          warn("Cannot find uuid for dependency $dep\n");
        }
      } else {
        if (defined($pkgs{$dep})) {
          my $depver = $pkgs{$dep}{'revision'};
          print { $fd{'depends'} } "$pkg,$ver,$dep,$depver\n";
        } else {
          print "Cannot find dependency $dep\n";
        }
      }
    }
  }

  for my $f (keys %fls) {
    print { $fd{'File'} } "\"$f\"\n";
    for my $pkg (keys %{$fls{$f}{'contained'}}) {
      my $t = $fls{$f}{'contained'}{$pkg};
      if ($opt_initial) {
        my $cuuid = $pkgs{$pkg}{'uuid'};
        if ($cuuid) {
          print { $fd{'includes'} } "$cuuid,$t,\"$f\"\n";
        } else {
          warn("Cannot find uuid for dependency $pkg for file $f\n");
        }
      } else {
        my $pver = $pkgs{$pkg}{'revision'};
        print { $fd{'includes'} } "$pkg,$pver,$t,\"$f\"\n";
      }
    }
  }


  for my $fd (keys %fd) {
    close($fd{$fd}) || warn "Cannot close fd for $fd: $!";
  }
}

__END__

### Local Variables:
### perl-indent-level: 2
### tab-width: 2
### indent-tabs-mode: nil
### End:
# vim:set tabstop=2 expandtab: #
